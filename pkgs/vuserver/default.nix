{
  lib,
  python3Packages,
  fetchFromGitHub,
  makeWrapper,
}:
python3Packages.buildPythonApplication rec {
  pname = "vuserver";
  version = "20240329";

  src = fetchFromGitHub {
    owner = "SasaKaranovic";
    repo = "VU-Server";
    rev = "v${version}";
    sha256 = "sha256-K2oJrqgNGRus5bvYHdhtyDQeHvCbW+fAlQLMn00Ovco=";
  };

  format = "other";

  nativeBuildInputs = [makeWrapper];

  propagatedBuildInputs = with python3Packages; [
    tornado
    numpy
    pillow
    requests
    pyyaml
    ruamel-yaml
    pyserial
  ];

  dontBuild = true;

  patchPhase = ''
    substituteInPlace dials/base_logger.py \
      --replace "logFile = f'/home/{getpass.getuser()}/KaranovicResearch/vudials/server.log'" \
                "logFile = os.path.join(os.environ.get('LOGSDIR'), 'vuserver.log')"
    substituteInPlace dials/base_logger.py \
      --replace "logFile = f'~/Library/Logs/KaranovicResearch/vudials/server.log'" \
                "logFile = os.path.join(os.environ.get('LOGSDIR'), 'vuserver.log')"
    substituteInPlace server.py \
      --replace "pid_file = os.path.join(os.path.dirname(os.path.abspath(__file__)), file_name)" \
                "pid_file = os.path.join(os.environ.get('RUNTIMEDIR'), 'pid')"
    substituteInPlace server.py \
      --replace "WEB_ROOT = os.path.join(BASEDIR_PATH, 'www')" \
                "WEB_ROOT = os.path.join(os.environ.get('RUNTIMEDIR'), 'www')"
    substituteInPlace database.py \
      --replace "database_path = os.path.join(os.path.dirname(__file__))" \
                "database_path = os.environ.get('STATEDIR')"
    substituteInPlace server_config.py \
      --replace "'port': 3000" \
                "'port': os.environ.get('PORT')"
    substituteInPlace server_config.py \
      --replace "'master_key': 'cTpAWYuRpA2zx75Yh961Cg'" \
                "'master_key': os.environ.get('KEY')"

    substituteInPlace serial_driver.py \
      --replace "import binascii" \
                "import binascii
import os
import sys"
    substituteInPlace serial_driver.py \
      --replace "while self.port.in_waiting:" \
                "try:
            port_in_waiting = self.port.in_waiting
        except OSError as e:
            if e.errno == 6:
                sys.exit(0)
            raise
        while port_in_waiting:"
  '';

  installPhase = ''
    mkdir -p "$out/lib"
    cp -r * "$out/lib"
    rm "$out/lib/config.yaml"

    makeWrapper \
      ${python3Packages.python.interpreter} \
      $out/bin/vuserver \
      --run "mkdir -p \$LOGSDIR \$STATEDIR" \
      --run "find \$RUNTIMEDIR -type f -exec chmod u+w {} + 2>/dev/null || true" \
      --run "find \$RUNTIMEDIR -type d -exec chmod u+w {} + 2>/dev/null || true" \
      --run "rm -rf \$RUNTIMEDIR 2>/dev/null || true" \
      --run "mkdir -p \$RUNTIMEDIR" \
      --run "cp -R $out/lib/www \$RUNTIMEDIR/www" \
      --run "chmod -R u+w \$RUNTIMEDIR" \
      --run "if [ ! -f \$STATEDIR/key ] || [ ! -s \$STATEDIR/key ]; then export KEY=\$(head /dev/urandom | tr -dc A-Za-z0-9 | head -c 64) && echo \$KEY > \$STATEDIR/key; fi" \
      --run "export KEY=\$(cat \$STATEDIR/key)" \
      --run "echo \"const API_MASTER_KEY = '\$KEY';\" > \$RUNTIMEDIR/www/assets/js/vu1_gui_root.js.tmp" \
      --run "sed 1d \$RUNTIMEDIR/www/assets/js/vu1_gui_root.js >> \$RUNTIMEDIR/www/assets/js/vu1_gui_root.js.tmp" \
      --run "mv \$RUNTIMEDIR/www/assets/js/vu1_gui_root.js.tmp \$RUNTIMEDIR/www/assets/js/vu1_gui_root.js" \
      --chdir "$out/lib" \
      --add-flags "$out/lib/server.py" \
      --set PYTHONPATH "$PYTHONPATH:$out/lib"

    wrapPythonPrograms
  '';

  doCheck = false;

  meta = with lib; {
    description = "VU Server for controlling VU dials (cross-platform)";
    homepage = "https://github.com/SasaKaranovic/VU-Server";
    license = licenses.mit;
    platforms = platforms.unix;
  };
}
